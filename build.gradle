plugins {
    id 'java'
    id 'net.ellune.blossom' version '1.0.1'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
}

blossom {
    String path = 'src/main/java/io/github/myessentials/mytown2/PluginInfo.java'
    replaceToken '@project.version@', project.version, path
    replaceToken '@project.id@', project.id, path
    replaceToken '@project.name@', project.name, path
    replaceToken '@project.description@', project.description, path
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    compile 'org.spongepowered:spongeapi:4.1.0'
    shade 'com.github.Flibio:Updatifier:v1.4.0:api'
}

jar {
    manifest {
        attributes 'Implementation-Title': archivesBaseName, 'Implementation-Version': version
    }
    configurations.shade.copyRecursive().setTransitive(false).each { artifact -> from (zipTree(artifact))}
}

task apiJar(type: Jar) {
    classifier = 'api'
    from sourceSets.main.output
    include 'io/github/myessentials/mytown2/api/**/*'
}

task apiSourceJar(type: Jar) {
    classifier = 'api-sources'
    from sourceSets.main.allJava
    include 'io/github/myessentials/mytown2/api/**/*'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives apiJar
    archives apiSourceJar
    archives sourceJar
}
